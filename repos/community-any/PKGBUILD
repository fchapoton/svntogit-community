# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: lynix <lynix47@gmail.com>
# Derived from 'dina-font-otb' by Ckat <ckat@teknik.io>

pkgname=terminus-font-otb
pkgver=4.49
pkgrel=1
pkgdesc='Monospace bitmap font (OTB version)'
url='http://terminus-font.sourceforge.net/'
arch=('any')
license=('GPL2' 'custom:OFL')
makedepends=('fontforge')
source=("https://downloads.sourceforge.net/project/terminus-font/terminus-font-$pkgver/terminus-font-$pkgver.tar.gz"
        'otbconvert.pe')
sha256sums=('db0a74551a23d2229db6ffea469ca8e390625ace4069dd1c1a3d05a16ed2634d'
            '431999be6be2630a9e8b4bcc5631b6bc727b795978b75cb4c874d9ce143e9703')

build() {
    cd "terminus-font-$pkgver"
    ../otbconvert.pe *.bdf
}

package() {
    cd "terminus-font-$pkgver"
    for i in *.otb; do
        install -D -m0644 $i "$pkgdir/usr/share/fonts/misc/$i"
    done
    install -D -m0644 'OFL.TXT' "$pkgdir/usr/share/licenses/terminus-font-otb/LICENSE"
}
