# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: lynix <lynix47@gmail.com>
# Derived from 'dina-font-otb' by Ckat <ckat@teknik.io>

pkgname=terminus-font-otb
pkgver=4.49
pkgrel=2
pkgdesc='Monospace bitmap font (OTB version)'
url='http://terminus-font.sourceforge.net/'
arch=('any')
license=('GPL2' 'custom:OFL')
makedepends=('fontforge')
source=("https://downloads.sourceforge.net/project/terminus-font/terminus-font-$pkgver/terminus-font-$pkgver.tar.gz")
sha256sums=('db0a74551a23d2229db6ffea469ca8e390625ace4069dd1c1a3d05a16ed2634d')

build() {
  cd "terminus-font-$pkgver"

  ./configure \
    --x11dir=/usr/share/fonts/misc/
  make otb
}

package() {
  make -C "terminus-font-$pkgver" DESTDIR="$pkgdir" install-otb

  install -Dm644 "$srcdir/terminus-font-$pkgver/OFL.TXT" \
    "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
